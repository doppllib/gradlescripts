task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
      if(project.ext.has("javaVersion")){
        mavenJava(MavenPublication) {
          version = project.ext.get("javaVersion")
          from components.java
          artifactId project.ext.has("artifactId") ? project.ext.get("artifactId") : project.name
          artifact sourcesJar
        }
      }
      mavenDoppl(MavenPublication) {
          artifact(dopplArchive)
          artifactId project.ext.has("artifactId") ? project.ext.get("artifactId") : project.name
          artifact sourcesJar
          pom.withXml {
              def dependencies = asNode().appendNode('dependencies')
              configurations.getByName("doppl").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                  def dependency = dependencies.appendNode('dependency')
                  dependency.appendNode('groupId', it.moduleGroup)
                  dependency.appendNode('artifactId', it.moduleName)
                  dependency.appendNode('version', it.moduleVersion)
              }
          }
      }
    }
    repositories {
        maven {
            url dopplMavenDeploy
        }
    }
}
